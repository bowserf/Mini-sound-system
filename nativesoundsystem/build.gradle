apply plugin: 'com.android.model.native'

model {
    android {
        compileSdkVersion rootProject.ext.compileSdkVersion

        // This allows the library project to be built in debug mode, so we can actually debug it
        publishNonDefault true
    }

    android.ndk {
        moduleName = "soundsystem"
        ldLibs.addAll(["log", "android", "OpenSLES"])
        abiFilters.addAll(["armeabi", "armeabi-v7a", "x86"])
        CFlags.add("-std=c99")
        cppFlags.add("-std=c++11")
        stl "stlport_static"
        platformVersion = rootProject.ext.minSdkVersion //same as minSdkVersion.apiLevel for better compatibility
        if (rootProject.ext.minSdkVersion >= 24) {
            ldLibs.addAll(["mediandk"])
            cppFlags.addAll(["-DMEDIACODEC_EXTRACTOR"])
            CFlags.addAll(["-DMEDIACODEC_EXTRACTOR"])
        }else if(rootProject.ext.minSdkVersion >= 21) {
            cppFlags.addAll(["-DFLOAT_PLAYER"])
            CFlags.addAll(["-DFLOAT_PLAYER"])
        }
    }

    android.sources {
        main {
            jni {
                exportedHeaders {
                    srcDir "src/main/jni"
                }
            }
        }
    }
}

configurations {
    // Expose the debug version of the library to other modules
    debug
    release
}

