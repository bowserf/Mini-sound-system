apply plugin: 'com.android.model.native'

model {
    android {
        compileSdkVersion rootProject.ext.compileSdkVersion
    }

    android.ndk {
        moduleName = "soundsystem"
        ldLibs.addAll(["log", "android", "atomic", "OpenSLES"])
        abiFilters.addAll(["armeabi", "armeabi-v7a", "x86"])
        CFlags.add("-std=c99")
        cppFlags.add("-std=c++11")

        //same as minSdkVersion.apiLevel for better compatibility
        platformVersion = rootProject.ext.minSdkVersion

        toolchain "clang"
        stl "c++_static"

        if (rootProject.ext.minSdkVersion >= 26) {
            ldLibs.addAll(["mediandk", "aaudio"])
            cppFlags.addAll(["-DAAUDIO", "-DMEDIACODEC_EXTRACTOR"])
            CFlags.addAll(["-DAAUDIO", "-DMEDIACODEC_EXTRACTOR"])
        } else if (rootProject.ext.minSdkVersion >= 24) {
            ldLibs.addAll(["mediandk"])
            cppFlags.addAll(["-DMEDIACODEC_EXTRACTOR"])
            CFlags.addAll(["-DMEDIACODEC_EXTRACTOR"])
        } else if (rootProject.ext.minSdkVersion >= 21) {
            cppFlags.addAll(["-DFLOAT_PLAYER"])
            CFlags.addAll(["-DFLOAT_PLAYER"])
        }
    }

    android.sources {
        main {
            jni {
                exportedHeaders {
                    srcDir "src/main/jni"
                }
            }
        }
    }
}